{ config, lib, pkgs, ... }:

let
  inherit (lib) mkIf mkOption types;
  inherit (pkgs) fetchurl;

  inherit (pkgs.callPackage ../linux-package.nix { }) linuxPackage repos;

  cfg = config.microsoft-surface;

  version = "6.0.17";
  extraMeta.branch = "6.0";
  patchDir = repos.linux-surface + "/patches/${extraMeta.branch}";
  kernelPatches = pkgs.callPackage ./patches.nix {
    inherit patchDir;
  };
  structuredExtraConfig = pkgs.callPackage ./config.nix {
    inherit lib;
  };

  kernelPackages = linuxPackage {
    inherit version extraMeta kernelPatches structuredExtraConfig;
    src = fetchurl {
      url = "mirror://kernel/linux/kernel/v6.x/linux-${version}.tar.xz";
      sha256 = "sha256-p+6SCSpUWbtGq/C1RJpOV+i3klkaxOesBO0lQtLOHQg=";
    };
  };


in {
  options.microsoft-surface.kernelVersion = mkOption {
    type = types.enum [ "6.0.17" ];
  };

  config = mkIf (cfg.kernelVersion == "6.0.17") {
    boot = {
      inherit kernelPackages;
    };
  };
}
